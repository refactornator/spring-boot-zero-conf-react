plugins {
    id 'com.github.psxpaul.execfork' version '0.1.12'
    id 'com.moowork.node' version '1.3.1'
    id 'org.springframework.boot' version '2.1.8.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'java'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

bootRun {
    sourceResources sourceSets.main
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

node {
    version = '12.11.0'
    download = true
}

task installDependencies(type: NpmTask) {
    args = ['install', '--save-dev', 'webpack', 'webpack-cli']
}

task startNpmWatch(type: com.github.psxpaul.task.ExecFork) {
    inputs.file('webpack.config.js')
    inputs.dir('frontend')
    outputs.dir('src/main/resources/static')
    outputs.dir('src/main/resources/templates')

    executable = 'node_modules/.bin/webpack'
    args = [ '--watch' ]
    stopAfter = bootRun
    waitForOutput = 'Built at'
}

processResources.dependsOn 'startNpmWatch'
